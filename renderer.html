<!-- Code stolen from https://iwearshorts.com/blog/simple-shader-with-threejs/ !-->
<head>
    <script id="fragmentShader" type="x-shader/x-fragment">
        uniform float time;
        const vec2 iResolution = vec2(512, 228);
        const highp uvec2 iIntermediateResolution = uvec2(128, 128);
        uniform highp usampler2D dstMap;
        uniform highp sampler2D intermediate;

        void main()	{
            highp uvec3 bitParts = texture(dstMap, gl_FragCoord.xy / iResolution).xyz;
            highp uint x = bitParts.b;
            highp uint y = bitParts.g;
            highp ivec2 coords = ivec2(int(y), 127 - int(x));
            
            gl_FragColor = texelFetch(intermediate, coords, 0);
            //gl_FragColor = texture(intermediate, gl_FragCoord.xy / iResolution);
        }
    </script>
    <script id="vertexShader" type="x-shader/x-vertex">
        uniform float time;
        uniform vec2 resolution;
        void main()	{
            gl_Position = vec4( position, 1.0 );
        }
    </script>
    <script id="intfragmentShader" type="x-shader/x-fragment">
        uniform float time;
        const vec2 iIntResolution = vec2(128, 128);
        const vec2 iResolution = vec2(512, 228);
        uniform sampler2D srcMap;

        /*void main()	{
            vec2 fragCoord = texture(srcMap, gl_FragCoord.xy / iIntResolution).xy * iResolution;
            float x = mod(time + fragCoord.x, 20.) < 10. ? 1. : 0.;
            float y = mod(time + fragCoord.y, 20.) < 10. ? 1. : 0.;
            gl_FragColor = vec4(vec3(min(x, y)), 1.);
        }*/

        /*
        * "Seascape" by Alexander Alekseev aka TDM - 2014
        * License Creative Commons Attribution-NonCommercial-ShareAlike 3.0 Unported License.
        * Contact: tdmaav@gmail.com
        */

        const int NUM_STEPS = 8;
        const float PI	 	= 3.141592;
        const float EPSILON	= 1e-3;
        #define EPSILON_NRM (0.1 / iResolution.x)
        #define AA

        // sea
        const int ITER_GEOMETRY = 3;
        const int ITER_FRAGMENT = 5;
        const float SEA_HEIGHT = 0.6;
        const float SEA_CHOPPY = 4.0;
        const float SEA_SPEED = 0.8;
        const float SEA_FREQ = 0.16;
        const vec3 SEA_BASE = vec3(0.0,0.09,0.18);
        const vec3 SEA_WATER_COLOR = vec3(0.8,0.9,0.6)*0.6;
        #define SEA_TIME (1.0 + time * SEA_SPEED)
        const mat2 octave_m = mat2(1.6,1.2,-1.2,1.6);

        // math
        mat3 fromEuler(vec3 ang) {
            vec2 a1 = vec2(sin(ang.x),cos(ang.x));
            vec2 a2 = vec2(sin(ang.y),cos(ang.y));
            vec2 a3 = vec2(sin(ang.z),cos(ang.z));
            mat3 m;
            m[0] = vec3(a1.y*a3.y+a1.x*a2.x*a3.x,a1.y*a2.x*a3.x+a3.y*a1.x,-a2.y*a3.x);
            m[1] = vec3(-a2.y*a1.x,a1.y*a2.y,a2.x);
            m[2] = vec3(a3.y*a1.x*a2.x+a1.y*a3.x,a1.x*a3.x-a1.y*a3.y*a2.x,a2.y*a3.y);
            return m;
        }
        float hash( vec2 p ) {
            float h = dot(p,vec2(127.1,311.7));	
            return fract(sin(h)*43758.5453123);
        }
        float noise( in vec2 p ) {
            vec2 i = floor( p );
            vec2 f = fract( p );	
            vec2 u = f*f*(3.0-2.0*f);
            return -1.0+2.0*mix( mix( hash( i + vec2(0.0,0.0) ), 
                            hash( i + vec2(1.0,0.0) ), u.x),
                        mix( hash( i + vec2(0.0,1.0) ), 
                            hash( i + vec2(1.0,1.0) ), u.x), u.y);
        }

        // lighting
        float diffuse(vec3 n,vec3 l,float p) {
            return pow(dot(n,l) * 0.4 + 0.6,p);
        }
        float specular(vec3 n,vec3 l,vec3 e,float s) {    
            float nrm = (s + 8.0) / (PI * 8.0);
            return pow(max(dot(reflect(e,n),l),0.0),s) * nrm;
        }

        // sky
        vec3 getSkyColor(vec3 e) {
            e.y = (max(e.y,0.0)*0.8+0.2)*0.8;
            return vec3(pow(1.0-e.y,2.0), 1.0-e.y, 0.6+(1.0-e.y)*0.4) * 1.1;
        }

        // sea
        float sea_octave(vec2 uv, float choppy) {
            uv += noise(uv);        
            vec2 wv = 1.0-abs(sin(uv));
            vec2 swv = abs(cos(uv));    
            wv = mix(wv,swv,wv);
            return pow(1.0-pow(wv.x * wv.y,0.65),choppy);
        }

        float map(vec3 p) {
            float freq = SEA_FREQ;
            float amp = SEA_HEIGHT;
            float choppy = SEA_CHOPPY;
            vec2 uv = p.xz; uv.x *= 0.75;
            
            float d, h = 0.0;    
            for(int i = 0; i < ITER_GEOMETRY; i++) {        
                d = sea_octave((uv+SEA_TIME)*freq,choppy);
                d += sea_octave((uv-SEA_TIME)*freq,choppy);
                h += d * amp;        
                uv *= octave_m; freq *= 1.9; amp *= 0.22;
                choppy = mix(choppy,1.0,0.2);
            }
            return p.y - h;
        }

        float map_detailed(vec3 p) {
            float freq = SEA_FREQ;
            float amp = SEA_HEIGHT;
            float choppy = SEA_CHOPPY;
            vec2 uv = p.xz; uv.x *= 0.75;
            
            float d, h = 0.0;    
            for(int i = 0; i < ITER_FRAGMENT; i++) {        
                d = sea_octave((uv+SEA_TIME)*freq,choppy);
                d += sea_octave((uv-SEA_TIME)*freq,choppy);
                h += d * amp;        
                uv *= octave_m; freq *= 1.9; amp *= 0.22;
                choppy = mix(choppy,1.0,0.2);
            }
            return p.y - h;
        }

        vec3 getSeaColor(vec3 p, vec3 n, vec3 l, vec3 eye, vec3 dist) {  
            float fresnel = clamp(1.0 - dot(n,-eye), 0.0, 1.0);
            fresnel = pow(fresnel,3.0) * 0.5;
                
            vec3 reflected = getSkyColor(reflect(eye,n));    
            vec3 refracted = SEA_BASE + diffuse(n,l,80.0) * SEA_WATER_COLOR * 0.12; 
            
            vec3 color = mix(refracted,reflected,fresnel);
            
            float atten = max(1.0 - dot(dist,dist) * 0.001, 0.0);
            color += SEA_WATER_COLOR * (p.y - SEA_HEIGHT) * 0.18 * atten;
            
            color += vec3(specular(n,l,eye,60.0));
            
            return color;
        }

        // tracing
        vec3 getNormal(vec3 p, float eps) {
            vec3 n;
            n.y = map_detailed(p);    
            n.x = map_detailed(vec3(p.x+eps,p.y,p.z)) - n.y;
            n.z = map_detailed(vec3(p.x,p.y,p.z+eps)) - n.y;
            n.y = eps;
            return normalize(n);
        }

        float heightMapTracing(vec3 ori, vec3 dir, out vec3 p) {  
            float tm = 0.0;
            float tx = 1000.0;    
            float hx = map(ori + dir * tx);
            if(hx > 0.0) return tx;   
            float hm = map(ori + dir * tm);    
            float tmid = 0.0;
            for(int i = 0; i < NUM_STEPS; i++) {
                tmid = mix(tm,tx, hm/(hm-hx));                   
                p = ori + dir * tmid;                   
                float hmid = map(p);
                if(hmid < 0.0) {
                    tx = tmid;
                    hx = hmid;
                } else {
                    tm = tmid;
                    hm = hmid;
                }
            }
            return tmid;
        }

        vec3 getPixel(in vec2 coord, float time) {    
            vec2 uv = coord / iResolution.xy;
            uv = uv * 2.0 - 1.0;
            uv.x *= iResolution.x / iResolution.y;    
                
            // ray
            vec3 ang = vec3(sin(time*3.0)*0.1,sin(time)*0.2+0.3,time);    
            vec3 ori = vec3(0.0,3.5,time*5.0);
            vec3 dir = normalize(vec3(uv.xy,-2.0)); dir.z += length(uv) * 0.14;
            dir = normalize(dir) * fromEuler(ang);
            
            // tracing
            vec3 p;
            heightMapTracing(ori,dir,p);
            vec3 dist = p - ori;
            vec3 n = getNormal(p, dot(dist,dist) * EPSILON_NRM);
            vec3 light = normalize(vec3(0.0,1.0,0.8)); 
                    
            // color
            //return vec3(pow(smoothstep(0.0,-0.02,dir.y),0.2), 0, 0);
            //getSkyColor(dir);
            //getSeaColor(p,n,light,dir,dist)
            return mix(
                getSkyColor(dir),
                vec3(getSeaColor(p,n,light,dir,dist).r, 1, 1),
                pow(smoothstep(0.0,-0.02,dir.y),0.2));
        }

        // main
        void main() {
            float time = time * 0.3;
            vec2 fragCoord = (vec2(0.0, 1.0) - texture(srcMap, gl_FragCoord.xy / iIntResolution).zy) * vec2(-1.0, 1.0) * iResolution;
        #ifdef AA
            vec3 color = vec3(0.0);
            for(int i = -1; i <= 1; i++) {
                for(int j = -1; j <= 1; j++) {
                    vec2 uv = fragCoord+vec2(i,j)/3.0;
                    color += getPixel(uv, time);
                }
            }
            color /= 9.0;
        #else
            vec3 color = getPixel(fragCoord, time);
        #endif
            
            // post
            gl_FragColor = vec4(color, 1.0);
            //gl_FragColor = vec4(fragCoord / iResolution, 0, 1);
            //vec4 fragColor = vec4(pow(color, vec3(0.65, 0.65, 0.65)), 1.0);
            //gl_FragColor = fragColor;
        }
    </script>
</head>

<body>
    <script type="module">
        // Find the latest version by visiting https://unpkg.com/three. The URL will
        // redirect to the newest stable release.
        import * as THREE from 'https://unpkg.com/three@0.120.1/build/three.module.js';

        var container = document.createElement('div');
		document.body.appendChild(container);

        console.log(THREE)

        var camera = new THREE.Camera();
        camera.position.z = 1;

        const renderTarget = new THREE.WebGLRenderTarget(128, 128);
        renderTarget.texture.minFilter = THREE.NearestFilter;
        renderTarget.texture.magFilter = THREE.NearestFilter;

        const srcMapTex = THREE.ImageUtils.loadTexture( "src_map.png" );
        srcMapTex.minFilter = THREE.NearestFilter;
        srcMapTex.magFilter = THREE.NearestFilter;

        const dstMapTex = THREE.ImageUtils.loadTexture( "dst_map.png", undefined, () => { 
            dstMapTex.minFilter = THREE.NearestFilter;
            dstMapTex.magFilter = THREE.NearestFilter;
            dstMapTex.internalFormat = "RGB8UI";
            dstMapTex.format = THREE.RGBIntegerFormat;
            dstMapTex.needsUpdate = true;
        });
        console.log(dstMapTex.format, dstMapTex.type, THREE.RGBAFormat, THREE.UnsignedByteType)
        //dstMapTex.type = THREE.UnsignedIntType;

        /*
        * Set up the final scene
        */

        var scene = new THREE.Scene();

        var uniforms = {
            time: { type: "f", value: 0 },
            resolution: { type: "v2", value: new THREE.Vector2() },
            dstMap: { type: "t", value: dstMapTex },
            intermediate: { type: "t", value: renderTarget.texture },
        };

        var material = new THREE.ShaderMaterial( {
            uniforms: uniforms,
            vertexShader: document.getElementById( 'vertexShader' ).innerText,
            fragmentShader: document.getElementById( 'fragmentShader' ).innerText
        });

        var mesh = new THREE.Mesh( new THREE.PlaneGeometry( 2, 2 ), material );
        scene.add( mesh );

        /*
        * Set up the intermediate render target
        */

        var rtScene = new THREE.Scene();

        var rtUniforms = {
            time: { type: "f", value: 0 },
            resolution: { type: "v2", value: new THREE.Vector2() },
            srcMap: { type: "t", value: srcMapTex },
        };

        var rtMaterial = new THREE.ShaderMaterial( {
            uniforms: rtUniforms,
            vertexShader: document.getElementById( 'vertexShader' ).innerText,
            fragmentShader: document.getElementById( 'intfragmentShader' ).innerText
        });

        var rtMesh = new THREE.Mesh( new THREE.PlaneGeometry( 2, 2 ), rtMaterial );
        rtScene.add( rtMesh );

        var renderer = new THREE.WebGLRenderer({});
        renderer.setSize( 128, 128 );
        container.appendChild(renderer.domElement);

        uniforms.resolution.value.x = parent.clientWidth;
        uniforms.resolution.value.y = parent.clientHeight;
        var startTime = Date.now();

        animate();

        function animate() {
            requestAnimationFrame( animate );
            render();
        }

        function render() {
            var elapsedMilliseconds = Date.now() - startTime;
            var elapsedSeconds = elapsedMilliseconds / 1000.;
            rtUniforms.time.value = elapsedSeconds;

            renderer.setRenderTarget(renderTarget);
            renderer.setSize( 128, 128 );
            renderer.render( rtScene, camera );
            renderer.setRenderTarget(null);

            renderer.setSize( 512, 228 );
            renderer.render( scene, camera );
        }
    </script>
</body>

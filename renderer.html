<!-- Code stolen from https://iwearshorts.com/blog/simple-shader-with-threejs/ !-->
<head>
    <script id="stencilFragmentShader" type="x-shader/x-fragment">
        uniform float time;
        const vec2 iResolution = vec2(420, 236);
        uniform highp sampler2D stencilTexture;

        void main()	{
            vec4 color = texture(stencilTexture, gl_FragCoord.xy / iResolution);
            // warning discard is slow
            // do not use this shader every frame
            if (color.r < 0.5) {
                discard;
            } else {
                gl_FragColor = color;
            }
        }
    </script>
    <script id="fragmentShader" type="x-shader/x-fragment">
        uniform float time;
        const vec2 iResolution = vec2(420, 236);
        
        uniform vec2 focalOffset;
        uniform highp usampler2D dstMap1;
        uniform highp usampler2D dstMap2;
        uniform highp usampler2D dstMap3;
        uniform highp usampler2D dstMap4;
        uniform highp sampler2D intermediate;

        void main()	{
            highp uvec4 bitParts = texture(dstMap1, gl_FragCoord.xy / iResolution);
            ivec2 coords = ivec2(int(bitParts.r | (bitParts.g << 8)), int(bitParts.z));
            gl_FragColor = texelFetch(intermediate, coords, 0) * float(bitParts.a)/255.0;
            
            bitParts = texture(dstMap2, gl_FragCoord.xy / iResolution);
            coords = ivec2(int(bitParts.r | (bitParts.g << 8)), int(bitParts.z));
            gl_FragColor += texelFetch(intermediate, coords, 0) * float(bitParts.a)/255.0;

            bitParts = texture(dstMap3, gl_FragCoord.xy / iResolution);
            coords = ivec2(int(bitParts.r | (bitParts.g << 8)), int(bitParts.z));
            gl_FragColor += texelFetch(intermediate, coords, 0) * float(bitParts.a)/255.0;

            bitParts = texture(dstMap4, gl_FragCoord.xy / iResolution);
            coords = ivec2(int(bitParts.r | (bitParts.g << 8)), int(bitParts.z));
            gl_FragColor += texelFetch(intermediate, coords, 0) * float(bitParts.a)/255.0;
            //gl_FragColor = texture(intermediate, gl_FragCoord.xy / iResolution);
        }
    </script>
    <script id="vertexShader" type="x-shader/x-vertex">
        uniform float time;
        uniform vec2 resolution;
        void main()	{
            gl_Position = vec4( position, 1.0 );
        }
    </script>
    <script id="intfragmentShader" type="x-shader/x-fragment">
        uniform float iTime;
        const vec2 iResolution = vec2(420, 246);
        uniform vec2 focalOffset;
        uniform int iFrame;

        /*void main()	{
            vec2 fragCoord = texture(srcMap, gl_FragCoord.xy / iIntResolution).zy * iResolution;
            //vec2 fragCoord = gl_FragCoord.xy;
            float x = mod(time * 50.0 + fragCoord.x, 20.) < 10. ? 1. : 0.;
            float y = mod(time * 50.0 + fragCoord.y, 20.) < 10. ? 1. : 0.;
            gl_FragColor = vec4(vec3(min(x, y)), 1.);
        }*/

        // Protean clouds by nimitz (twitter: @stormoid)
    // https://www.shadertoy.com/view/3l23Rh
    // License Creative Commons Attribution-NonCommercial-ShareAlike 3.0 Unported License
    // Contact the author for other licensing options

    /*
	Technical details:

	The main volume noise is generated from a deformed periodic grid, which can produce
	a large range of noise-like patterns at very cheap evalutation cost. Allowing for multiple
	fetches of volume gradient computation for improved lighting.

	To further accelerate marching, since the volume is smooth, more than half the the density
	information isn't used to rendering or shading but only as an underlying volume	distance to 
	determine dynamic step size, by carefully selecting an equation	(polynomial for speed) to 
	step as a function of overall density (not necessarialy rendered) the visual results can be 
	the	same as a naive implementation with ~40% increase in rendering performance.

	Since the dynamic marching step size is even less uniform due to steps not being rendered at all
	the fog is evaluated as the difference of the fog integral at each rendered step.

    */

    mat2 rot(in float a){float c = cos(a), s = sin(a);return mat2(c,s,-s,c);}
    const mat3 m3 = mat3(0.33338, 0.56034, -0.71817, -0.87887, 0.32651, -0.15323, 0.15162, 0.69596, 0.61339)*1.93;
    float mag2(vec2 p){return dot(p,p);}
    float linstep(in float mn, in float mx, in float x){ return clamp((x - mn)/(mx - mn), 0., 1.); }
    float prm1 = 0.;
    vec2 bsMo = vec2(0);

    vec2 disp(float t){ return vec2(sin(t*0.22)*1., cos(t*0.175)*1.)*2.; }

    vec2 map(vec3 p)
    {
        vec3 p2 = p;
        p2.xy -= disp(p.z).xy;
        p.xy *= rot(sin(p.z+iTime)*(0.1 + prm1*0.05) + iTime*0.09);
        float cl = mag2(p2.xy);
        float d = 0.;
        p *= .61;
        float z = 1.;
        float trk = 1.;
        float dspAmp = 0.1 + prm1*0.2;
        for(int i = 0; i < 5; i++)
        {
            p += sin(p.zxy*0.75*trk + iTime*trk*.8)*dspAmp;
            d -= abs(dot(cos(p), sin(p.yzx))*z);
            z *= 0.57;
            trk *= 1.4;
            p = p*m3;
        }
        d = abs(d + prm1*3.)+ prm1*.3 - 2.5 + bsMo.y;
        return vec2(d + cl*.2 + 0.25, cl);
    }

    vec4 render( in vec3 ro, in vec3 rd, float time )
    {
        vec4 rez = vec4(0);
        const float ldst = 8.;
        vec3 lpos = vec3(disp(time + ldst)*0.5, time + ldst);
        float t = 1.5;
        float fogT = 0.;
        for(int i=0; i<130; i++)
        {
            if(rez.a > 0.99)break;

            vec3 pos = ro + t*rd;
            vec2 mpv = map(pos);
            float den = clamp(mpv.x-0.3,0.,1.)*1.12;
            float dn = clamp((mpv.x + 2.),0.,3.);
            
            vec4 col = vec4(0);
            if (mpv.x > 0.6)
            {
            
                col = vec4(sin(vec3(5.,0.4,0.2) + mpv.y*0.1 +sin(pos.z*0.4)*0.5 + 1.8)*0.5 + 0.5,0.08);
                col *= den*den*den;
                col.rgb *= linstep(4.,-2.5, mpv.x)*2.3;
                float dif =  clamp((den - map(pos+.8).x)/9., 0.001, 1. );
                dif += clamp((den - map(pos+.35).x)/2.5, 0.001, 1. );
                col.xyz *= den*(vec3(0.005,.045,.075) + 1.5*vec3(0.033,0.07,0.03)*dif);
            }
            
            float fogC = exp(t*0.2 - 2.2);
            col.rgba += vec4(0.06,0.11,0.11, 0.1)*clamp(fogC-fogT, 0., 1.);
            fogT = fogC;
            rez = rez + col*(1. - rez.a);
            t += clamp(0.5 - dn*dn*.05, 0.09, 0.3);
        }
        return clamp(rez, 0.0, 1.0);
    }

    float getsat(vec3 c)
    {
        float mi = min(min(c.x, c.y), c.z);
        float ma = max(max(c.x, c.y), c.z);
        return (ma - mi)/(ma+ 1e-7);
    }

    //from my "Will it blend" shader (https://www.shadertoy.com/view/lsdGzN)
    vec3 iLerp(in vec3 a, in vec3 b, in float x)
    {
        vec3 ic = mix(a, b, x) + vec3(1e-6,0.,0.);
        float sd = abs(getsat(ic) - mix(getsat(a), getsat(b), x));
        vec3 dir = normalize(vec3(2.*ic.x - ic.y - ic.z, 2.*ic.y - ic.x - ic.z, 2.*ic.z - ic.y - ic.x));
        float lgt = dot(vec3(1.0), ic);
        float ff = dot(dir, normalize(ic));
        ic += 1.5*dir*sd*ff*lgt;
        return clamp(ic,0.,1.);
    }

    void main()
    {	
        vec2 fragCoord = gl_FragCoord.xy;
        vec2 q = fragCoord.xy/iResolution.xy;
        vec2 p = (gl_FragCoord.xy - 0.5*iResolution.xy)/iResolution.y;
        //bsMo = (iMouse.xy - 0.5*iResolution.xy)/iResolution.y;
        
        float time = iTime*3.;
        vec3 ro = vec3(0,0,time);
        
        ro += vec3(sin(iTime)*0.5,sin(iTime*1.)*0.,0);
            
        float dspAmp = .85;
        ro.xy += disp(ro.z)*dspAmp;
        float tgtDst = 3.5;
        
        vec3 target = normalize(ro - vec3(disp(time + tgtDst)*dspAmp, time + tgtDst));
        ro.x -= bsMo.x*2.;
        vec3 rightdir = normalize(cross(target, vec3(0,1,0)));
        vec3 updir = normalize(cross(rightdir, target));
        rightdir = normalize(cross(updir, target));
        vec3 rd=normalize((p.x*rightdir + p.y*updir)*1. - target);
        rd.xy *= rot(-disp(time + 3.5).x*0.2 + bsMo.x);
        prm1 = smoothstep(-0.4, 0.4,sin(iTime*0.3));
        vec4 scn = render(ro, rd, time);
            
        vec3 col = scn.rgb;
        col = iLerp(col.bgr, col.rgb, clamp(1.-prm1,0.05,1.));
        
        col = pow(col, vec3(.55,0.65,0.6))*vec3(1.,.97,.9);

        col *= pow( 16.0*q.x*q.y*(1.0-q.x)*(1.0-q.y), 0.12)*0.7+0.3; //Vign
        
        vec4 fragColor = vec4( col, 1.0 );
        gl_FragColor = fragColor;
    }
    </script>
</head>

<body>
    <div id='fpsMeter'></div>
    <script type="module">
        // Find the latest version by visiting https://unpkg.com/three. The URL will
        // redirect to the newest stable release.
        import * as THREE from 'https://unpkg.com/three@0.120.1/build/three.module.js';

        var container = document.createElement('div');
		document.body.appendChild(container);

        var camera = new THREE.Camera();
        camera.position.z = 1;

        const renderTarget = new THREE.WebGLRenderTarget(420, 236, { stencilBuffer: true });
        renderTarget.texture.minFilter = THREE.NearestFilter;
        renderTarget.texture.magFilter = THREE.NearestFilter;

        const maskTexture = THREE.ImageUtils.loadTexture( "mask.png" );
        maskTexture.minFilter = THREE.NearestFilter;
        maskTexture.magFilter = THREE.NearestFilter;

        const dstMapTex1 = THREE.ImageUtils.loadTexture( "dst_map_1.png", undefined, () => { 
            dstMapTex1.minFilter = THREE.NearestFilter;
            dstMapTex1.magFilter = THREE.NearestFilter;
            dstMapTex1.internalFormat = "RGBA8UI";
            dstMapTex1.format = THREE.RGBAIntegerFormat;
            dstMapTex1.needsUpdate = true;
        });
        const dstMapTex2 = THREE.ImageUtils.loadTexture( "dst_map_2.png", undefined, () => { 
            dstMapTex2.minFilter = THREE.NearestFilter;
            dstMapTex2.magFilter = THREE.NearestFilter;
            dstMapTex2.internalFormat = "RGBA8UI";
            dstMapTex2.format = THREE.RGBAIntegerFormat;
            dstMapTex2.needsUpdate = true;
        });
        const dstMapTex3 = THREE.ImageUtils.loadTexture( "dst_map_3.png", undefined, () => { 
            dstMapTex3.minFilter = THREE.NearestFilter;
            dstMapTex3.magFilter = THREE.NearestFilter;
            dstMapTex3.internalFormat = "RGBA8UI";
            dstMapTex3.format = THREE.RGBAIntegerFormat;
            dstMapTex3.needsUpdate = true;
        });
        const dstMapTex4 = THREE.ImageUtils.loadTexture( "dst_map_4.png", undefined, () => { 
            dstMapTex4.minFilter = THREE.NearestFilter;
            dstMapTex4.magFilter = THREE.NearestFilter;
            dstMapTex4.internalFormat = "RGBA8UI";
            dstMapTex4.format = THREE.RGBAIntegerFormat;
            dstMapTex4.needsUpdate = true;
        });
        //dstMapTex.type = THREE.UnsignedIntType;

        /*
        * Set up the final scene
        */

        var scene = new THREE.Scene();

        var uniforms = {
            time: { type: "f", value: 0 },
            resolution: { type: "v2", value: new THREE.Vector2() },
            focalOffset: { type: "v2", value: new THREE.Vector2(0, 0) },
            dstMap1: { type: "t", value: dstMapTex1 },
            dstMap2: { type: "t", value: dstMapTex2 },
            dstMap3: { type: "t", value: dstMapTex3 },
            dstMap4: { type: "t", value: dstMapTex4 },
            intermediate: { type: "t", value: renderTarget.texture },
        };

        var material = new THREE.ShaderMaterial( {
            uniforms: uniforms,
            vertexShader: document.getElementById( 'vertexShader' ).innerText,
            fragmentShader: document.getElementById( 'fragmentShader' ).innerText
        });

        var mesh = new THREE.Mesh( new THREE.PlaneGeometry( 2, 2 ), material );
        scene.add( mesh );

        /*
        * Set up the stencil buffer scene
        */

        var stScene = new THREE.Scene();

        var stUniforms = {
            time: { type: "f", value: 0 },
            resolution: { type: "v2", value: new THREE.Vector2() },
            stencilTexture: { type: "t", value: maskTexture },
        };

        var stMaterial = new THREE.ShaderMaterial( {
            uniforms: stUniforms,
            vertexShader: document.getElementById( 'vertexShader' ).innerText,
            fragmentShader: document.getElementById( 'stencilFragmentShader' ).innerText
        });

        var stMesh = new THREE.Mesh( new THREE.PlaneGeometry( 2, 2 ), stMaterial );
        stScene.add( stMesh );

        /*
        * Set up the intermediate render target
        */

        var rtScene = new THREE.Scene();

        var rtUniforms = {
            iTime: { type: "f", value: 0 },
            resolution: { type: "v2", value: new THREE.Vector2() },
            iFrame: { type: "i", value: 0 },
        };

        var rtMaterial = new THREE.ShaderMaterial( {
            uniforms: rtUniforms,
            vertexShader: document.getElementById( 'vertexShader' ).innerText,
            fragmentShader: document.getElementById( 'intfragmentShader' ).innerText
        });

        var rtMesh = new THREE.Mesh( new THREE.PlaneGeometry( 2, 2 ), rtMaterial );
        rtScene.add( rtMesh );

        var renderer = new THREE.WebGLRenderer({});
        renderer.setSize( 420, 236 );
        renderer.autoClearStencil = false;
        container.appendChild(renderer.domElement);

        var gl = renderer.context;

        uniforms.resolution.value.x = parent.clientWidth;
        uniforms.resolution.value.y = parent.clientHeight;
        var startTime = Date.now();

        animate();

        function animate() {
            requestAnimationFrame( animate );
            render();
        }

        var lastTime = new Date().getTime();
        var tot = 0;
        var num = 0;
        function render() {
            var elapsedMilliseconds = Date.now() - startTime;
            var elapsedSeconds = elapsedMilliseconds / 1000.;
            rtUniforms.iTime.value = elapsedSeconds;
            rtUniforms.iFrame.value += rtUniforms.iFrame.value;

            renderer.setRenderTarget(renderTarget);
            renderer.setClearColor( 0xffffff, 1);
            renderer.clear(true, true, true);
            gl.enable(gl.STENCIL_TEST);
            // the third argument here allows us to write to the stencil buffer
            gl.stencilOp(gl.KEEP, gl.KEEP, gl.REPLACE);

            renderer.clear(); //<-- works without this too

            gl.stencilFunc(gl.ALWAYS, 1, 0xFF);
            gl.stencilMask(0xFF);
            renderer.render(stScene, camera);

            gl.stencilFunc(gl.EQUAL, 1, 0xFF);
            gl.stencilMask(0x00);

            gl.colorMask(true, true, true, true);
            renderer.render( rtScene, camera );

            gl.stencilMask(0xFF);
            gl.disable(gl.STENCIL_TEST);
            //renderer.render(stScene, camera);
            renderer.setRenderTarget(null);

            renderer.render( scene, camera );
            //renderer.render( rtScene, camera );

            var now = new Date().getTime();
            //console.log(1000 / (now - lastTime));
            num += 1;
            tot += (1000/(now - lastTime))
            document.getElementById('fpsMeter').innerHTML = "fps: " + (tot/num)
            lastTime = now;
        }
    </script>
</body>
